#include <WiFi.h>
#include <HTTPClient.h>
#include <ArduinoJson.h>
#include "DHTesp.h"

int pinDHT = 15;
DHTesp dht;

int relay = 13; // relay pin
int soilMoisturePin = 34; // Soil moisture sensor pin

const char* SSID = "tum";
const char* PASSWORD = "oooooooo";
const char* LINE_TOKEN = "SyMyIczyA9X83N90l6vrLMI0Jdpl8GOgwaQAVrCGo2v";
const char* LINE_API_URL = "https://notify-api.line.me/api/notify";

int soilMoistureThreshold = 3395; // Threshold for soil moisture 

// task for core 0 (DHT and Soil Moisture sensor)
void core0Task(void * pvParameters) {
  HTTPClient http;
  while (true) {
    vTaskDelay(pdMS_TO_TICKS(300000)); // delay this task
    if (WiFi.status() == WL_CONNECTED) {
      // à¸­à¹ˆà¸²à¸™à¸„à¹ˆà¸²à¸ˆà¸²à¸ DHT
      TempAndHumidity data = dht.getTempAndHumidity();
      Serial.printf("Humidity: %.2f%%, Temperature: %.1fÂ°C\n", data.humidity, data.temperature);
      
      // à¸­à¹ˆà¸²à¸™à¸„à¹ˆà¸²à¸„à¸§à¸²à¸¡à¸Šà¸·à¹‰à¸™à¹ƒà¸™à¸”à¸´à¸™
      int soilMoistureValue = analogRead(soilMoisturePin);
      Serial.printf("Soil Moisture Value: %d\n", soilMoistureValue);
      
      // à¸•à¸£à¸§à¸ˆà¸ªà¸­à¸šà¸„à¹ˆà¸²à¸„à¸§à¸²à¸¡à¸Šà¸·à¹‰à¸™à¹ƒà¸™à¸”à¸´à¸™
      if (soilMoistureValue > soilMoistureThreshold) {
        Serial.println("Soil is too dry! Sending notification...");
        sendLineNotification("à¸„à¸§à¸²à¸¡à¸Šà¸·à¹‰à¸™à¹ƒà¸™à¸”à¸´à¸™à¸•à¹ˆà¸³à¹€à¸à¸´à¸™à¹„à¸›! ðŸŒ±ðŸ’§ à¸à¸£à¸¸à¸“à¸²à¸£à¸”à¸™à¹‰à¸³à¸•à¹‰à¸™à¹„à¸¡à¹‰"); 
      }
      
      // à¸ªà¹ˆà¸‡à¸„à¹ˆà¸²à¹„à¸›à¸¢à¸±à¸‡ API
      if (http.begin("https://tnj-development.com/hightech/backend/API/getReq.php")) {
        http.addHeader("Content-Type", "application/x-www-form-urlencoded");
        int httpResponseCode = http.POST("humidity=" + String(data.humidity, 2) +
                                         "&temperature=" + String(data.temperature, 1) +
                                         "&soilMoisture=" + String(soilMoistureValue));
        if (httpResponseCode > 0) {
          Serial.printf("HTTP POST response code: %d\n", httpResponseCode);
        } else {
          Serial.printf("HTTP POST request failed, error: %s\n", http.errorToString(httpResponseCode).c_str());
        }
        http.end();
      } else {
        Serial.println("Unable to connect to server");
      }
    }
  }
}

// task for core 1 (Watering control via API)
void core1Task(void * pvParameters) {
  HTTPClient http;
  while (true) {
    vTaskDelay(pdMS_TO_TICKS(1500)); // à¸«à¸¢à¸¸à¸” Task à¸™à¸µà¹‰à¹„à¸§à¹‰ 1.5 à¸§à¸´à¸™à¸²à¸—à¸µ
    if (WiFi.status() == WL_CONNECTED) {
      if (http.begin("https://tnj-development.com/hightech/backend/API/watering.json")) {
        int httpResponseCode = http.GET();
        if (httpResponseCode > 0) {
          String payload = http.getString();
          StaticJsonDocument<200> doc;
          DeserializationError error = deserializeJson(doc, payload);
          if (!error) {
            int sec = doc["sec"];
            if (sec >= 1000) {
              digitalWrite(relay, HIGH); // à¹€à¸›à¸´à¸”à¸£à¸µà¹€à¸¥à¸¢à¹Œ
              vTaskDelay(pdMS_TO_TICKS(sec)); // à¸£à¸­ sec à¸§à¸´à¸™à¸²à¸—à¸µ
              digitalWrite(relay, LOW); // à¸›à¸´à¸”à¸£à¸µà¹€à¸¥à¸¢à¹Œ
              sendLineNotification("à¸£à¸”à¸™à¹‰à¸³à¸•à¹‰à¸™à¹„à¸¡à¹‰à¹€à¸ªà¸£à¹‡à¸ˆà¹€à¸£à¸µà¸¢à¸šà¸£à¹‰à¸­à¸¢à¹à¸¥à¹‰à¸§ðŸ’¦ðŸŒ²");

              HTTPClient httpClose;
              if (httpClose.begin("https://tnj-development.com/hightech/backend/API/closeWatering.api.php")) {
                httpClose.addHeader("Content-Type", "application/x-www-form-urlencoded");
                int closeResponseCode = httpClose.POST("command=watered&sec=" + String(sec));
                if (closeResponseCode > 0) {
                  Serial.printf("HTTP POST response code for closing: %d\n", closeResponseCode);
                } else {
                  Serial.printf("HTTP POST request failed for closing, error: %s\n", httpClose.errorToString(closeResponseCode).c_str());
                }
                httpClose.end();
              } else {
                Serial.println("Unable to connect to close watering server");
              }
            }
          } else {
            Serial.print("deserializeJson() failed: ");
            Serial.println(error.c_str());
          }
        } else {
          Serial.printf("HTTP GET request failed with code: %d\n", httpResponseCode);
        }
        http.end();
      } else {
        Serial.println("Unable to connect to watering.json server");
      }
    }
  }
}

void setup() {
  Serial.begin(115200);
  // relay setup
  pinMode(relay, OUTPUT);
  digitalWrite(relay, LOW);

  // wifi setup
  WiFi.begin(SSID, PASSWORD);
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.print(".");
  }
  Serial.println("\nWiFi connected");

  dht.setup(pinDHT, DHTesp::DHT11);
  pinMode(soilMoisturePin, INPUT);

  xTaskCreatePinnedToCore(core0Task, "Core0Task", 8192, NULL, 1, NULL, 0);
  xTaskCreatePinnedToCore(core1Task, "Core1Task", 8192, NULL, 1, NULL, 1);
}

void loop() {
  // this loop is required
}

void sendLineNotification(String message) {
  if (WiFi.status() == WL_CONNECTED) {
    HTTPClient http;
    http.begin(LINE_API_URL);
    http.addHeader("Authorization", "Bearer " + String(LINE_TOKEN));
    http.addHeader("Content-Type", "application/x-www-form-urlencoded");

    String payload = "message=" + message;
    int httpResponseCode = http.POST(payload);
    if (httpResponseCode > 0) {
      Serial.println("Notification sent successfully");
    } else {
      Serial.println("Error in sending notification");
    }
    http.end();
  } else {
    Serial.println("Error: Not connected to WiFi");
  }
}
